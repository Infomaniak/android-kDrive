apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'realm-android'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.infomaniak.drive"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 40001801
        versionName "4.0.18"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'CLIENT_ID', '"9473D73C-C20F-4971-9E10-D957C563FA68"'
        buildConfigField 'String', 'DRIVE_API', '"https://drive.infomaniak.com/drive/"'
        buildConfigField 'String', 'INFOMANIAK_API', '"https://api.infomaniak.com/1/"'
        buildConfigField 'String', 'OFFICE_URL', '"https://drive.infomaniak.com/app/office/"'
        buildConfigField 'String', 'SHOP_URL', '"https://shop.infomaniak.com/order/"'
        buildConfigField 'Boolean', 'BETA', 'true' // Todo

        resValue 'string', 'ACCOUNT_TYPE', 'com.infomaniak.drive'
        resValue 'string', 'CLOUD_STORAGE_AUTHORITY', 'com.infomaniak.drive.documents'
        resValue 'string', 'SYNC_AUTHORITY', 'com.infomaniak.drive.sync.provider'
        resValue 'string', 'FILE_AUTHORITY', 'com.infomaniak.drive.files'

        resValue 'string', 'EXPOSED_UPLOAD_DIR', 'upload_media'
    }

    compileOptions {
        sourceCompatibility rootProject.ext.java_version
        targetCompatibility rootProject.ext.java_version
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.java_version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "distribution"
    productFlavors {
        standard {
            dimension "distribution"
            apply plugin: 'com.google.gms.google-services'
        }
        fdroid {
            dimension "distribution"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':Core')

    def work_version = '2.5.0'
    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'

    def exoplayer_version = '2.14.2'
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer:$exoplayer_version"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayer_version"

    implementation 'com.airbnb.android:lottie:3.7.0'
    implementation 'com.github.MikeOrtiz:TouchImageView:3.1.0'
    implementation 'com.github.rubensousa:gravitysnaphelper:2.2.1'
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:4.1.2'
    implementation 'io.coil-kt:coil:1.3.0'
    implementation 'io.coil-kt:coil-gif:1.3.0'

    standardImplementation 'com.google.android.play:core:1.10.0'
    standardImplementation 'com.google.firebase:firebase-messaging-ktx:22.0.0'

    implementation 'io.sentry:sentry-android:5.0.1'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'
    testImplementation 'io.github.serpro69:kotlin-faker:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.work:work-testing:$work_version"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}